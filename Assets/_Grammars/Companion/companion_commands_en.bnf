#BNF+EMV2.1;
!grammar companion_commands_en;
!start <alarm>;
!start <battery>;
!start <buddylab>;
!start <calculationgame>;
!start <canmove>;
!start <colourseen>;
!start <copy>;
!start <dance>;
!start <date>;
!start <demoshort>;
!start <demofull>;
!start <detectobject>;
!start <dontmove>;
!start <dosomething>;
!start <false>;
!start <freezedance>;
!start <followme>;
!start <guardian>;
!start <headdown>;
!start <headleft>;
!start <headright>;
!start <headup>;
!start <heat>;
!start <hour>;
!start <icouldnt>;
!start <ihateyou>;
!start <iloveyou>;
!start <installallapp>;
!start <iot>;
!start <joke>;
!start <jukebox>;
!start <kissme>;
!start <lookatme>;
!start <memorygame>;
!start <mirror>;
!start <mood>;
!start <movebackward>;
!start <moveforward>;
!start <moveleft>;
!start <moveright>;
!start <nap>;
!start <photo>;
!start <play>;
!start <pose>;
!start <profilingwholikes>;
!start <profilingwhoisborn>;
!start <profilingnextbirthday>;
!start <profilingalluser>;
!start <profilingwhat>;
!start <profilinghowold>;
!start <propose>;
!start <recipe>;
!start <repeat>;
!start <rlgl>;
!start <story>;
!start <tellsomething>;
!start <timer>;
!start <true>;
!start <volume>;
!start <volumedown>;
!start <volumeup>;
!start <wander>;
!start <welcome>;
!start <sick>;
!start <sad>;
!start <lovely>;
!start <happy>;
!start <tired>;
!start <angry>;
!start <grumpy>;
!start <scared>;
!start <knockknock>;
!start <operation>;
!start <radio>;
!start <operationagain>;
!start <cancel>;
!start <accraactivity>;
!start <accraalarm>;
!start <quit>;
!import <numbers#nb>;
!import <names#anyname>;

<asklaunch> : [<canyou>] (open | launch | play) [[the] application];
<askgive> : [<canyou>] give (me | us) ;
<asktell> : [<canyou>] (tell | find | give) (me | us) | I want to hear;
<canyou> : (can | could) you;


<alarm> : (set | program) an alarm;
<battery> : [<askgive>] [your] battery [level];
<buddylab> : [<asklaunch>] ( buddylab | [the] lab | buddy lab | [the] laboratory);
<calculationgame> : [<asklaunch>] (calculation | [the] calculation game | playmath);
<canmove> : you can move;
<colour> : blue | white | red | green | yellow | black | brown | orange | purple | grey | pink | cyan | magenta;
<colourseen> : what is the (color | colour) of this object;
<copy> : copy (my moves | my movements | my move | my movement);
<dance> : [can you | I want you to | let's] dance;
<date> : what (day | date) are we | [what is | give me] the date;
<demoshort> : introduce yourself quickly | show [us | me] (quickly what you can do | what you can do (quickly | shortly )) | show off shortly | [show a] short demo;
<demofull> : introduce yourself | show [us | me] what you can do | show off | show a demo;
<detectobject> : do you see (something | an object) [in front of you];
<dontmove> : don't move | stop moving | stop;
<dosomething> : (do | show me) something;
<false> : wrong | incorrect;
<freezedance> : [<asklaunch>] freezedance | I (want | would like) to dance;
<followme> : track me | follow [me] | come with me;
<guardian> : [<asklaunch>] gardian | [<canyou>] [start] [the] monitoring;
<headdown> : ([move (the | your)] head | look) [<rotationangle>] down;
<headleft> : (turn (the | your) head | look) [<rotationangle>] [(on | to) the] left;
<headright> : (turn (the | your) head | look) [<rotationangle>] [(on | to) the] right;
<headup> : ([move (the | your)] head | look) [<rotationangle>] up | lift up your head;
<heat> : what (heat do | is the heat that) you detect;
<hour> : what time is it | give [me | us] the time | what is the time;
<icouldnt> : I couldn't | I could not | I didn't manage to;
<ihateyou> : I (hate | despite | don't like)  you | you (sucks | are (a loser | [so] bad | mean | stupid | a stupid robot)) | you are not funny;
<iloveyou> : I love you | I really like you |  I like playing with you | [you are a] good boy | you are (nice | smart);
<installallapp> : (install | get) (all [apps | applications]);  
<iot> : iot | connected objects | connected object | smart house | internet of things | <iot_temperature> | <iot_lights> | <iot_blinds> | <iot_music> | <iot_music_volume>;
<iot_temperature> : (raise | lower) the temperature | (set | change | turn) the (temperature | thermostat) [up | down] to <numbers#nb> [degrees] | make it (warmer | cooler) | what is the temperature in the ([living] room | house);
<iot_lights> : (turn | switch) (on | off) ([the] (light | plug) [(of | in) the (living | office) room] | all the lights);
<iot_blinds> : (open | close | lower | raise) (the blind [(of | in) the (living | office) room] | all the blinds);
<iot_music> : (play | stop | (turn (on | off))) ([my | the | some] music (on sonos [speaker] | in the living room) | the speaker (of | in) the living room) | [put | set] [the] (next | previous) song [in the living room];
<iot_music_volume> : (set the music volume to <numbers#nb> | turn up the music (volume | sound) | turn the music (volume | sound) up) [in the living room];
<joke> : [<asktell>] (a joke | a funny story | something funny);
<jukebox> : jukebox | music application | play music; 
<kissme> : kiss me |  I want a kiss | give me a kiss;
<lookatme> : look (at me | over there);
<memorygame> : [<asklaunch>] memory (game | application);
<mirror> : show me what you see | mirror;
<mood> : [what is | what's] [your] mood;
<movebackward> : ((go | move) [<numbers#nb> meters] backward | step back);
<moveforward> : ((go | move) [<numbers#nb> meters] forward | go straight);
<moveleft> : (move | turn | go | rotate) [<rotationangle>] [(on | to) the] left;
<moveright> : (move | turn | go | rotate) [<rotationangle>] [(on | to) the] right;
<nap> : take a nap | go to sleep;
<photo> : [can you take | let's take] (a photo | a picture | pictures | photos);
<pose> : (can I take | I would like) a (picture | photo) with you | (can we take | let's take) (a selfie | a (picture | photo) together);
<profilingwholikes> : [<asktell>] who likes (<colour> | <sport> | <colour> and <sport> | <sport> and <colour>);
<profilingwhoisborn> : [<asktell>] who is born (after | before | in) <numbers#nb>;
<profilingnextbirthday> : [<asktell>] (when | who | what) is the next birthday;
<profilingalluser> : [<asktell>] (all | what [do] | something) [you know] about (<names#anyname> | him | her);
<profilingwhat> : [<asktell>] what is (<names#anyname>'s | the family's | the) ( (favorite (colour | sport)) [of the family] | birthdate);
<profilinghowold> : [<asktell>] (How old is (<names#anyname> | he | she) | what is (<names#anyname>'s | her | his) age);
<propose> : propose [me | us] something;
<play> : [let's | I want to | I wanna] play | propose a game | random game;
<recipe> : recipe;
<repeat> : I didn't hear you [well] | I didn't understand | can you (repeat | say [it] again) [please] | [sorry] what did you say | repeat;
<rlgl> : [<asklaunch>] (red light green light);
<sport> : (basket | basetball) | (foot | football) | handball | rugby | tennis | ping pong | table tennis | cricket | swimming | running | archery | fencing | curling | climbing;
<story> : [<asktell>] a story;
<tellsomething> : tell me something | [tell me a] fun fact;
<timer> : set a (timer | countdown) [of <delai>];
<true> : true | correct | that's right | right | indeed;
<volume> : (what is | what's | give [me | us]) the volume [level];
<volumeup> : turn up the (volume | sound) | turn the (volume | sound) up | speak louder;
<volumedown> : turn down the (volume | sound) | turn the (volume | sound) down | lower (the volume | your voice);
<wander> : (walk | wander) [around] | go away | go for a (walk | ride) | (take | have) a stroll;
<welcome> : welcome;
<show> : show [me | us] when you are | be;
<sick> : [<show>] sick;
<sad> : [<show>] sad;
<lovely> : [<show>] (lovely | in love);
<happy> : [<show>] (happy | joy | joyful);
<tired> : [<show>] tired;
<angry> : [<show>] excited;
<grumpy> : [<show>] grumpy;
<scared> : [<show>] scared;
<knockknock> : knock knock;
<delai> : <numbers#nb> (seconds | minutes | hours);
<rotationangle> : <numbers#nb> degrees;
<radioname> : RTL2 | France Maghreb 2 | Virgin Radio | RMC | Radio Nova | Radio Meuh | Fun Radio | Tropiques FM | FIP | Jazz Radio | Europe 1 | Djam Radio | Chérie | Hotmixradio 80 | RTL | Rire et Chansons | Latina | Radio Public Santé | franceinfo | Chante France | Skyrock | La Grosse Radio | Beur FM | RFM | NRJ | Africa numéro 1 | Alouette | Sud Radio | OUI FM | M Radio | Radio Orient | Nostalgie | RADIO FG | BFM Business | France Culture | Voltage | France Bleu Paris | France Inter | Radio Classique | France Musique;
<radio> : [<asklaunch>] ([the] radio | [[the] radio] <radioname>);
<operation> : [(how much are | what is)] <numbers#nb> (plus | minus | divided by | multiplied by | times) <numbers#nb>;
<operationagain> :  (plus | minus | divided by | multiplied by | times) <numbers#nb>;
<cancel> : (cancel | delete) [all] the timer;
<accraactivity> : [<canyou> | (I | we) (want to | would like [you] to) | can I] (launch | start | do | make) (an exercice | an activity | a session);
<accraalarm> : Call for help | Alert (an helper | someone) | I'm not really well | I have a problem;
<quit> : quit | cancel | stop | exit | that's all | back | end | leave;