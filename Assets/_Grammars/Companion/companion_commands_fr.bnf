#BNF+EMV2.1;
!grammar companion_commands_fr;
!start <alarm>;
!start <battery>;
!start <buddylab>;
!start <calculationgame>;
!start <canmove>;
!start <colourseen>;
!start <copy>;
!start <dance>;
!start <date>;
!start <demoshort>;
!start <demofull>;
!start <detectobject>;
!start <dontmove>;
!start <dosomething>;
!start <false>;
!start <freezedance>;
!start <followme>;
!start <guardian>;
!start <headdown>;
!start <headleft>;
!start <headright>;
!start <headup>;
!start <heat>;
!start <hour>;
!start <icouldnt>;
!start <ihateyou>;
!start <iloveyou>;
!start <installallapp>;
!start <iot>;
!start <joke>;
!start <jukebox>;
!start <kissme>;
!start <lookatme>;
!start <memorygame>;
!start <mirror>;
!start <mood>;
!start <movebackward>;
!start <moveforward>;
!start <moveleft>;
!start <moveright>;
!start <nap>;
!start <photo>;
!start <play>;
!start <pose>;
!start <profilingwholikes>;
!start <profilingwhoisborn>;
!start <profilingnextbirthday>;
!start <profilingalluser>;
!start <profilingwhat>;
!start <profilinghowold>;
!start <propose>;
!start <recipe>;
!start <repeat>;
!start <rlgl>;
!start <story>;
!start <tellsomething>;
!start <timer>;
!start <true>;
!start <volume>;
!start <volumedown>;
!start <volumeup>;
!start <wander>;
!start <welcome>;
!start <sick>;
!start <sad>;
!start <lovely>;
!start <happy>;
!start <tired>;
!start <angry>;
!start <grumpy>;
!start <scared>;
!start <knockknock>;
!start <operation>;
!start <radio>;
!start <operationagain>;
!start <cancel>;
!start <accraactivity>;
!start <accraalarm>;
!start <quit>;
!import <numbers#nb>;
!import <names#anyname>;

<asklaunch> : ((ouvre | lance ) | (<canyou> (ouvrir | lancer)) | je voudrais [utiliser | jouer à | que tu (ouvres | lances)]) [[l'] application ];
<askgive> : (<canyou> [me | nous] (donner | dire)) | ((donne | dis) [moi | nous]) | (je (veux | voudrais) que tu [me | nous] (donnes | dises)) ;
<asktell> : raconte [moi | nous] | je (veux | voudrais) | (<canyou> [me | nous] (raconter | dire)) | <canyou> trouver | trouve;
<asktake> : ([<canyou>] [nous | me] | peut-on | pouvons-nous) (prendre | faire) | (prends [nous | moi] | fais) | prenons [nous] | on [se] prend | on fait;
<canyou> : (peux-tu | pourrais-tu | [est-ce que] tu (peux | pourrais));


<alarm> : ((je (veux | voudrais) que tu(lances | programmes | mettes)) | lance | programme | mets |  <canyou> (lancer | programmer | mettre)) une alarme;
<battery> : [<askgive>] [[ton | le] niveau de] batterie;
<buddylab> : [<asklaunch>] ( buddylab | [le] lab | buddy lab | [le] laboratoire);
<calculationgame> : [<asklaunch>] ([de] calcul | [le] jeu de calcul | playmath);
<canmove> : tu peux bouger;
<colour> : bleu | blanc | rouge | vert | jaune | noire | marron | orange | violet | gris | beige | rose;
<colourseen> : de quel couleur est cet objet;
<copy> : copie mes mouvements | fais comme moi | recopie moi;
<dance> : [je voudrais que tu | vas-y] danse | [<canyou> | sais-tu] danser;
<date> : quel jour sommes nous | <askgive> la date | quelle est la date | quel jour on est | quel jour est-on | on est quel jour;
<demoshort> : (montre | présente) [moi | nous] ce que tu sais faire rapidement | présente-toi rapidement | fais [moi | nous] une démo rapide;
<demofull> : (montre | présente) [moi | nous] ce que tu sais faire | présente-toi | fais [moi | nous] une démo;
<detectobject> : est-ce que tu vois (quelque chose | un objet) devant toi;
<dontmove> : [ne] bouge (plus | pas) | arrête-toi | arrête de bouger | stop;
<dosomething> : (fait | montre moi) quelque chose;
<false> : faux | incorrect | négatif;
<freezedance> : [<asklaunch>] freezedance | je (veux | voudrais) danser;
<followme> : suis-moi | viens avec moi;
<guardian> : [<asklaunch>] (gardien | [de] surveillance);
<headdown> : (baisse (la | ta) tête | regarde (( en | vers le | plus) bas | par terre) [<rotationangle>]);
<headleft> : (tourne la tête | regarde) ([<rotationangle>] (à | sur la | vers la) gauche | (à | sur la | vers la) gauche <rotationangle>);
<headright> : (tourne la tête | regarde) ([<rotationangle>] (à | sur la | vers la) droite | (à | sur la | vers la) droite <rotationangle>);
<headup> : (lève (la | ta) tête | regarde (( en | vers le | plus) haut | en l'air)) [<rotationangle>];
<heat> : quelle est la chaleur que tu detectes | quelle chaleur détectes-tu;
<hour> : quelle heure est-il | <askgive> l'heure | il est quelle heure | c'est quoi l'heure;
<icouldnt> : je n'ai pas (pu | su | réussi);
<ihateyou> : je te (hais | déteste) | je ne t'aime pas | tu es (nul | stupide | idiot | con | bête | naze | un loser) | tu n'es pas drôle | tu ne comprends rien;
<iloveyou> : je (t'aime | t'adore | t'apprécie) | tu es (sympa | sympathique | gentil | cool | mignon | intelligent) | j'aime jouer avec toi | Nathalie | Roméo et Juliette;
<installallapp> : (installe | récupère | télécharge) (toutes les applications | tout | les applications);  
<iot> : iot | objets connectés | maison intelligente | <iot_temperature> | <iot_lights> | <iot_blinds> | <iot_music> | <iot_music_volume>;
<iot_temperature> : (monte | descend | augmente | baisse) ([la] température | le thermostat) [de <numbers#nb> degrés] | (règle | mets) (la température | le thermostat) (à | sur) <numbers#nb> degrés | quelle est la température dans (la maison | le salon);
<iot_lights> : (allume | éteins | allumer | éteindre) [la | toutes les] (lumière | prise) [du (salon | bureau)];
<iot_blinds> : (ouvre | ferme | monte | baisse) (le store [du (salon | bureau)] | tous les stores);
<iot_music> : (joue | mets | stop | arrête | lance | allume) (([de] la | ma) musique ((sur [l'enceinte] sonos | du salon) | dans le salon) | l'enceinte du salon) | [mets | passe à] ([le] morceau (suivant | précédent) | [la] chanson (suivante | précédente)) [dans le salon];
<iot_music_volume> : met le volume de la musique à <numbers#nb> [dans le salon] | (monte | baisse) [le (volume | son) de] la musique [(dans le | du) salon];
<joke> : <asktell> (une blague | une histoire (drôle | amusante | marrante) | quelque chose (de drôle | d'amusant | de marrant));
<jukebox> : [<asklaunch>] (jukebox | [la] musique); 
<kissme> : embrasse moi | [fais moi | je veux] (un | des) bisou | bisou | embrassons nous;
<lookatme> : regarde (moi | par ici);
<memorygame> : [<asklaunch>] [[le] jeu] [de] mémoire;
<mirror> : montre-moi ce que tu vois | miroir;
<mood> : [<askgive> | quelle est] [ton] humeur;
<movebackward> : (recule | reculer | va (en arrière | vers l'arrière)) [de <numbers#nb> mètres];
<moveforward> : (avance | avancer | va (vers l'avant | tout droit)) [de <numbers#nb> mètres];
<moveleft> : (tourne | tourner | va | fais une rotation) ([<rotationangle>] (à | sur la | vers la) gauche | (à | sur la | vers la) gauche <rotationangle>);
<moveright> : (tourne | tourner | va | fais une rotation) ([<rotationangle>] (à | sur la | vers la) droite | (à | sur la | vers la) droite <rotationangle>);
<nap> : (va dormir | repose toi | dort) [pendant <delai>] | (va faire | fait) une sieste [(de | pendant) <delai>];
<photo> : [<asktake>] [en | une] photo;
<play> : je (veux | voudrais) jouer | (<canyou> [me] proposer | propose [moi] | faisons) un jeu | jouons;
<pose> : ([<asktake>] (un selfie | une photo avec toi | une photo ensemble)) | prend la pose;
<profilingwholikes> : [<asktell>] qui aime le (<colour> | <sport> | <colour> et le <sport> | <sport> et le <colour>);
<profilingwhoisborn> : [<asktell>] qui est né (après | avant | en) <numbers#nb>;
<profilingnextbirthday> : [<asktell>] ( (quel | quand | qui) est le prochain | qui fêtera prochainement son) anniversaire;
<profilingalluser> : [<asktell>] [tout] ce que tu sais sur (<names#anyname> | lui | elle);
<profilingwhat> : [<asktell>] quel est ( (la couleur | le sport) [préféré | favori] [de (<names#anyname> | la famille) ] | la date de naissance [de <names#anyname>] | sa date de naissance | (sa couleur | son sport) [préféré | favori]);
<profilinghowold> : [<asktell>] (quel (âge a | est l'âge de) <names#anyname> | quel (est son âge | âge (il a | a-t-il)) );
<propose> : propose [moi | nous] quelque chose;
<recipe> : recette | application recette | mes recettes;
<repeat> : répète | je n'ai pas entendu | je n'ai pas compris | pas compris | redis | répéter | qu'est ce que tu as dit | qu'est ce que tu dis | tu as dit quoi;
<rlgl> : [<asklaunch>] (un deux trois soleil | 1 2 3 soleil);
<sport> : (basket | basetball) | (foot | football) [en salle] | handball | rugby | tennis | ping pong | tennis de table | cricket | natation | course à pieds | pétanque | tir à l'arc | escrime | curling | escalade;
<story> : <asktell> une histoire;
<tellsomething> : dis [moi | nous] quelque chose;
<timer> : [mets | <canyou> mettre | je (veux | voudrais) que tu mettes] ([un] (minuteur | décompte | compte à rebours | timer) | [une] minuterie) [de <delai>];
<true> : c'est exact | vrai | correct | affirmatif | exact | c'est très juste | en effet;
<volume> : (quel est | donne [moi | nous]) le [niveau (de | du)] volume;
<volumedown> : ((descend | diminue | baisse | baisser | diminuer | descendre) [le] (volume | son)) [de <numbers#nb>] | (mets [le] (volume | son) moins fort) | (parle moins fort);
<volumeup> : ((augmenter | augmente | monte | monter) [le] (volume | son)) [de <numbers#nb>] | (mets [le] (volume | son) plus fort) | (parle plus fort);
<wander> : [va te] (balader | promener) | va t'en | pars | (balade | promène) [toi];
<welcome> : bienvenue;
<show> : montre [moi | nous] quand tu es | sois;
<sick> : [<show>] malade;
<sad> : [<show>] triste;
<lovely> : [<show>] (adorable | amoureux);
<happy> : [<show>] (heureux | joyeux | content);
<tired> : [<show>] fatigué;
<angry> : [<show>] énervé;
<grumpy> : [<show>] grognon;
<scared> : [<show>] effrayé;
<knockknock> : toc toc;
<delai> : <numbers#nb> (secondes | minutes | heures);
<rotationangle> : [de] <numbers#nb> degrés;
<radioname> : RTL2 | France Maghreb 2 | Virgin Radio | RMC | Radio Nova | Radio Meuh | Fun Radio | Tropiques FM | FIP | Jazz Radio | Europe 1 | Djam Radio | Chérie | Hotmixradio 80 | RTL | Rire et Chansons | Latina | Radio Public Santé | franceinfo | Chante France | Skyrock | La Grosse Radio | Beur FM | RFM | NRJ | Africa numéro 1 | Alouette | Sud Radio | OUI FM | M Radio | Radio Orient | Nostalgie | RADIO FG | BFM Business | France Culture | Voltage | France Bleu Paris | France Inter | Radio Classique | France Musique;
<radio> : [<asklaunch>] ([la] radio | [[la] radio] <radioname>);
<operation> : [combien (font | ça fait)]  <numbers#nb> (plus | moins | divisé par | multiplié par | fois) <numbers#nb>;
<operationagain> : (plus | moins | divisé par | multiplié par | fois) <numbers#nb>;
<cancel> : (supprime | enlève) [tous] les (minuteur | décompte | compte à rebours | timer);
<accraactivity> : (([<canyou> | je (veux | voudrais | peux) | puis-je | on (veut | voudrait)] (lancer | faire | démarrer)) | (lance | fait | démarre)) (un exercice | une activité | une séance);
<accraalarm> : (Préviens | Appelle | Alerte) ((de | à) l'aide | du secours | un aidant | quelqu'un) | Je [ne] vais [vraiment] pas bien | Je vais [vraiment] [très] mal | J'ai un problème | Je suis tombé;
<quit> : quitter | annuler | arrêter | sortir | terminer | annule | quitte | ferme | arrête | arrêt | ce sera tout;