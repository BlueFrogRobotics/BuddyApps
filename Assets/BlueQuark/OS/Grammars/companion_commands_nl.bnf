#BNF+EMV2.1;
!grammar companion_commands_nl;

!start <battery>;
!start <canmove>;
!start <dance>;
!start <date>;
!start <demoshort>;
!start <dontmove>;
!start <followme>;
!start <gallery>;
!start <headdown>;
!start <headleft>;
!start <headright>;
!start <headup>;
!start <hour>;
!start <ihateyou>;
!start <iloveyou>;
!start <joke>;
!start <kissme>;
!start <mood>;
!start <movebackward>;
!start <moveforward>;
!start <moveleft>;
!start <moveright>;
!start <nap>;
!start <photo>;
!start <registerreminder>;
!start <repeat>;
!start <tellsomething>;
!start <volume>;
!start <volumedown>;
!start <volumeup>;
!start <wander>;
!start <sick>;
!start <sad>;
!start <love>;
!start <happy>;
!start <tired>;
!start <angry>;
!start <grumpy>;
!start <scared>;
!start <operation>;
!start <operationagain>;
!start <fitness>;
!start <quit>;
!import <numbers#nb>;

<battery> :  [is (je | de)] batterij [nog |bijna] [(vol | leeg)] | [moet je (bijna | al)] opladen | batterij [level | niveau];
<canmove> : [je kunt] bewegen | beweeg [maar];
<dance> : [ga (maar | eens)] [voor (mij | me | ons)] dansen | dans [maar];
<date> : [wat is de] datum [van vandaag] | (welke | wat voor) dag [is het] vandaag ;
<demoshort> : [geef] [een] (demo | demonstratie) | laat [me | ons | mij | eens] zien wat je [allemaal] (kunt |  kan | doet);
<dontmove> : niet bewegen | sta stil | beweeg niet;
<followme> : volg [me | mij] | kom [eens] [maar] [eens] achter (me | mij) aan | kom [met (me | mij)] mee | ga [met (me | mij)] mee;
<gallery> : [ik wil | mag ik] [mijn | het | jouw | ons] foto album [zien | bekijken | kijken] | laat [me | mij | ons] [mijn | de | die | onze] foto's zien | ga naar [het | mijn] [foto] (album | gallerij | verzameling | collectie) | [ik wil  | mag ik | kan ik | wij willen | mogen wij | kunnen wij] [mijn | jouw | onze] foto's (kijken | zien | bekijken);
<headdown> : ([(beweeg | kijk | draai | buig) (het | jouw | je)] hoofd | kijk) [<rotationangle>] [eens | maar] [naar] beneden | [doe je] hoofd omlaag; 
<headleft> : (draai (het | jouw | je) hoofd | kijk) [eens | maar] [<rotationangle>] [naar] links;
<headright> : (draai (het | jouw | je) hoofd | kijk) [eens | maar] [<rotationangle>] [naar] rechts;
<headup> : ([beweeg (het | jouw | je)] hoofd | kijk) [eens | maar] [<rotationangle>] [naar] (omhoog | boven) | [doe je] hoofd omhoog;
<hour> : hoe laat is het [eigenlijk] |ik wil weten hoe laat het [eigenlijk] is | vertel [me | mij] hoe laat het is | klok | wat is de tijd | de tijd;
<ihateyou> : Ik hou niet van jou | ik vind jou stom | jij bent (stom | dom | gemeen | gek | een stomme robot) | jij bent niet grappig;  
<iloveyou> : Ik hou van (jou | je) | ik vind je [erg] leuk | ik vind het leuk om [met je] te spelen | ik vind je [erg | heel] lief | jij bent (aardig | slim);
<joke> : vertel [me | mij |ons] [eens] (een mop | een grap | een raadsel | (iets | wat) grappigs) |(kun | wil) (je | jij) (een mop | een grap | een raadsel | (iets | wat) grappigs | (iets | wat) leuks) vertellen | (mop | moppen | raadsel | raadsels | grap | grappen) [vertellen];
<kissme> : knuffel me | Ik wil een [dikke] (kus | kusje | zoen | zoentje | knuffel) | geef [me | mij] [eens] een [dikke] (kus | kusje | zoen | zoentje | knuffel);
<mood> : wat is [je | jouw] humeur | in (wat voor | welke) stemming ben [je | jij] | hoe voel [je | jij] je | gaat het goed [met (je | jou)] | wat is [je | jouw] gevoel | (welke | wat voor | wat voor een | wat voor soort) stemming heb je | stemming | humeur;<movebackward> : ga [recht] achteruit;
<moveforward> : ga [recht] vooruit;
<moveleft> : draai [naar] links;
<moveright> : draai [naar] rechts;
<nap> : (ga [maar] even slapen | doe [maar] een dutje | ga [maar] [even] een dutje doen) [(voor | van | gedurende) <delai>];
<photo> : [(kun | wil | kan) (je | jij)] (foto | een foto | foto's) maken | [ik wil | wij willen] (foto | een foto | foto's) maken | fotograferen;
<registerreminder> : [kan | kun | wil (je | jij) | ik wil [graag]] [een] herinnering [toevoegen | bewaren | opslaan];
<repeat> : Ik hoorde je niet [goed] | Ik begreep het niet | (kun | wil) je ([het] herhalen | [het] nog een keer zeggen) | [sorry] wat zei je | herhaal [alsjeblieft] | nog een keer [alsjeblieft];
<tellsomething> : vertel me iets [leuks | grappigs] | [vertel me een] [leuk | grappig] weetje | wist je dat | nog een weetje; 
<volume> : Wat is het volume | geef me het volume | volumeniveau | hoe hard sta je;
<volumeup> : volume omhoog | geluid omhoog | geluid harder | volume harder;
<volumedown> : volume lager | geluid zachter | volume zachter;
<wander> : rondlopen | weggaan | wandelen | een ritje maken | een wandeling maken | een wandeling maken | rondwandelen | ronddwalen | rondrijden | een rondje lopen;
<show> : laat [me | mij | ons] zien;
<shownl>: doe eens | kijk eens | doe maar | kijk maar;
<sick> : <show> [heel] [erg] (ziek | misselijk) | <shownl> [heel] [erg] (ziek | misselijk);
<sad> : <show> [heel] [erg] (verdrietig | sip) | <shownl> [heel] [erg] (verdrietig | sip);
<love> : <show> (verliefd |lief | schattig) | <shownl> (verliefd | lief | schattig);
<happy> : <show> [heel] [erg] (blij | vrolijk | opgewekt) | <shownl> [heel] [erg] (blij | vrolijk | opgewekt);
<tired> : <show> [heel] [erg] (moe | vermoeid | erg moe |heel moe | uitgeput | [een] beetje moe) | <shownl> [heel] [erg] (moe | vermoeid | erg moe |heel moe | uitgeput | [een] beetje moe);  
<angry> : <show> (boos | kwaad) | <shownl> [heel] [erg] (boos | kwaad);
<grumpy> : <show> (chagrijnig | slecht humeur | met het verkeerde been uit bed gestapt) | <shownl> of je (chagrijnig bent | een slecht humeur hebt | met het verkeerde been uit bed bent gestapt | met het verkeerde been uit bed gestapt bent);
<scared> : <show> (geschrokken | angstig | bang);
<delai> : <numbers#nb> (seconden | minuten | uren);
<rotationangle> : <numbers#nb> graden;
<operation> : [(hoe veel is | wat is)] <numbers#nb> (plus | min | minus | gedeeld door | vermenigvuldigd met | keer) <numbers#nb>;
<operationagain> :  (plus | minus | min | gedeeld door | vermenigvuldigd met | keer) <numbers#nb>;
<fitness> : (we | wij) willen (graag | wel) | [kan ik] [een] (oefening | activiteit | sessie | training | sport) (starten | beginnen | doen | maken) | (ik wil | wij willen) bewegen | sporten | trainen | [een] workout [doen] | oefenen | een oefening doen | oefening | oefeningen;  
<quit> : Stop | hou op |  stop daarmee;
