#BNF+EMV2.0;
!grammar recurrence_fr;
!import <duration_fr#duration_years>;
!import <duration_fr#duration_months>;
!import <duration_fr#duration_weeks>;
!import <duration_fr#duration_days>;
!import <duration_fr#duration_hours>;
!import <duration_fr#duration_minutes>;
!import <duration_fr#duration_hoursandminutes>;
 
!start <every_years>;
!start <every_months>;
!start <every_weeks>;
!start <every_days>;
!start <every_hours>;
!start <every_minutes>;
!start <every_hoursandminutes>;
!start <every_daysofweeks>;
!start <just_once>;

<every_years> : [(tous | toutes) les] (ans | années | <duration_fr#duration_years>);
<every_months> : [tous les] (mois | <duration_fr#duration_months>);
<every_weeks> : [toutes les] (semaines | <duration_fr#duration_weeks>);
<every_days> : [tous les] (jours | <duration_fr#duration_days>);
<every_hours> : [toutes les] (heures | <duration_fr#duration_hours>);
<every_minutes> : [toutes les] (minutes | <duration_fr#duration_minutes>);
<every_hoursandminutes> : [toutes les] (<duration_fr#duration_hoursandminutes>);

<every_daysofweeks> : [[tous] les] (<dayofweek>) [!repeat([et] <dayofweek>, 1, 6)];
<dayofweek> : (lundi | mardi | mercredi | jeudi | vendredi | samedi | dimanche);

<just_once> : juste une fois | aucun | jamais | (ne pas répéter | ne répète pas | ne répète jamais | ne jamais répéter | je ne veux pas que tu (répètes | renouvelles)) [(cette alarme | ce rappel)] | je ne veux pas que ((cette alarme | ce rappel) (se répète | soit répété)) | non;
