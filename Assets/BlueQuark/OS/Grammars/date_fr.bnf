#BNF+EMV2.0;
!grammar date_fr;
!import <numbers#nb>;

/*
**	Date rules to export
*/

!export <date_fr#today>;
!export <date_fr#tomorrow>;
!export <date_fr#dayaftertomorrow>;
!export <date_fr#nextdayofweek>;
!export <date_fr#nextweek>;
!export <date_fr#nextmonth>;
!export <date_fr#nextyear>;
!export <date_fr#nextday>;
!export <date_fr#nextdate>;
!export <date_fr#fulldate>;

!start <today>;
!start <tomorrow>;
!start <dayaftertomorrow>;
!start <nextdayofweek>;
!start <nextweek>;
!start <nextmonth>;
!start <nextyear>;
!start <nextday>;
!start <nextdate>;
!start <fulldate>;

<today> : aujourd'hui;
<tomorrow> : demain;
<dayaftertomorrow> : après-demain;
<nextdayofweek> : <dayofweek> [prochain];
<nextweek> : la semaine prochaine;
<nextmonth> : le mois prochain;
<nextyear> : l'année prochaine | l'an prochain;
<nextday> : le (premier | <dayofmonthwithone>);
<nextdate> : [le] <dayofmonthwithfirst> <month>;
<fulldate> : <nextdate> <year> | [le] <dayofmonthwithone> <monthnumbers> <year>;
<dayofweek> : (lundi | mardi | mercredi | jeudi | vendredi | samedi | dimanche);
<dayofmonthwithfirst> : premier | <dayofmonthincomplete> ;
<dayofmonthwithone> : 1 | <dayofmonthincomplete> ;
<dayofmonthincomplete> : 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9 | 10 | 11 | 12 | 13 | 14 | 15 | 16 | 17 | 18 | 19 | 20 | 21 | 22 | 23 | 24 | 25 | 26 | 27 | 28 | 29 | 30 | 31;
<month> : janvier | février | mars | avril | mai | juin | juillet | août | septembre | octobre | novembre | décembre;
<monthnumbers> : 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9 | 10 | 11 | 12;
<year> : <numbers#nb>;