#BNF+EMV2.1;
!grammar companion_commands_it;
!start <battery>;
!start <canmove>;
!start <dance>;
!start <date>;
!start <demoshort>;
!start <demofull>;
!start <dontmove>;
!start <dosomething>;
!start <gallery>;
!start <headdown>;
!start <headleft>;
!start <headright>;
!start <headup>;
!start <heat>;
!start <hour>;
!start <ihateyou>;
!start <iloveyou>;
!start <joke>;
!start <kissme>;
!start <mood>;
!start <movebackward>;
!start <moveforward>;
!start <moveleft>;
!start <moveright>;
!start <nap>;
!start <photo>;
!start <play>;
!start <propose>;
!start <registerreminder>;
!start <repeat>;
!start <tellsomething>;
!start <volume>;
!start <volumedown>;
!start <volumeup>;
!start <wander>;
!start <welcome>;
!start <sick>;
!start <sad>;
!start <lovely>;
!start <happy>;
!start <tired>;
!start <angry>;
!start <grumpy>;
!start <scared>;
!start <operation>;
!start <operationagain>;
!start <quit>;
!import <numbers#nb>;

<askgive> : ([mi | ci] <canyou>  (dare | dire)) | (dammi | dacci | dimmi | dicci) | (io (voglio | vorrei) che [mi | ci] (dessi | dicessi)) ;
<asktell> : racconta [mi | ci] | io (voglio | vorrei) | (<canyou>  (raccontar | dir) [mi | ci]) | <canyou>  trovare | trova;
<asktake> : [<canyou>]  (prendere | fare) | (prendi [ci | mi] | fai) | prendiamo [ci] | [si] prende | fa;
<canyou> : (puoi | potresti | possiamo);


<battery> : [<askgive> | qual è] [il tuo | il] livello [di | della tua] batteria;
<canmove> : puoi muoverti;
<dance> : [vorrei che | per favore] balla | [<canyou> | sai] ballare;
<date> : che giorno è | <askgive> la data di oggi | che data è | in che giorno siamo;
<demoshort> : (mostra | illustra) [mi | ci] brevemente quello che sai fare | presentati velocemente | [dammi | dacci] una dimostrazione rapida;
<demofull> : (mostra | illustra) [mi | ci] cosa sai fare | presentati | [dammi | dacci] una dimostrazione;
<dontmove> : [non] spostarti | fermati | smetti di muoverti;
<dosomething> : (fa | mostrami) qualcosa;
<gallery> : ([<canyou> (aprire | entrare | avviare) | (apri | (entra | vai) nella | avvia) ] [la] galleria) | ( [<canyou>] ( mostrarmi | visualizzare ) | (posso vedere ) | ( mostra | visualizza )) ( le | le mie) ( foto | immagini ) ; 
<headdown> : (abbassa (la | la tua) testa | guarda (( in | verso il | più in) basso | per terra) [<rotationangle>]);
<headleft> : (gira la testa | guarda) ([<rotationangle>] (a | sulla | verso la) sinistra | (a | sulla | verso la) sinistra <rotationangle>);
<headright> : (gira la testa | guarda) ([<rotationangle>] (a | sulla | verso la) destra | (a | sulla | verso la) destra <rotationangle>);
<headup> : (alza (la | la tua) testa | guarda (( in | verso il | più in) alto | in aria)) [<rotationangle>];                                                                                              
<heat> : che temperatura hai | quanti gradi hai;
<hour> : che ora è | <askgive> l'ora | che ore sono;
<ihateyou> : ti (odio | detesto) | non mi piaci | sei (inutile | stupido | idiota | pazzo | cretino | imbecille | sfigato) | non sei divertente | non capisci nulla;
<iloveyou> :(mi piaci | ti adoro | ti stimo) | sei (simpatico | divertente | gentile | figo | carino | intelligente) | mi piace giocare con te | Nathalie | Romeo e Giulietta;
<joke> : <asktell> (una barzelletta | una storia (divertente | spassosa | buffa) | qualcosa di (divertente | spassoso | buffo));
<kissme> : baciami | [dammi | voglio] (uno | dei) baci | bacini | baciamoci;
<mood> : [<askgive> | di che] umore sei;
<movebackward> : (indietreggia | indietreggiare | va all'indietro) [di <numbers#nb> metri];
<moveforward> : (procedi | procedere | va (in avanti | sempre dritto)) [di <numbers#nb> metri];
<moveleft> : (gira | girare | vai | ruota) ([<rotationangle>] (a  | sulla | verso la) sinistra | (a  | sulla | verso la) sinistra <rotationangle>);
<moveright> : (gira | girare | vai | ruota) [<rotationangle>] (a  | sulla | verso la) destra | (a  | sulla | verso la) destra;
<nap> : (vai a dormire | riposati | dormi) [per <delai>] | (vai a fare | fai) un riposino[(di | per) <delai>];
<photo> : [<asktake>] una foto;
<play> : (voglio | vorrei) giocare | (<canyou> propormi | proponimi | facciamo) un gioco | giochiamo;
<propose> : proponi [mi | ci] qualcosa;
<registerreminder> : [[Puoi] (aggiungere| riaggiungere | registrare | mettere) | aggiungi| riaggiungi | registra | metti] [un] promemoria;
<repeat> : ripeti | non ho sentito | non ho capito | non compreso | ridillo | ripetere | cosa hai detto | cosa dice;
<tellsomething> : di [mi | ci] qualcosa;
<volume> : (qual è |[dimmi | dicci]) il [livello del] volume;
<volumedown> : ((abbassa | diminuisci | abbassare | diminuire) [il] volume | suono) [di <numbers#nb>] | (metti [i] (volume | suono) meno forte) | (parla più piano);
<volumeup> : ((aumentare | aumenta | alza | alzare) [il] (volume | suono)) [di <numbers#nb>] | (metti [il] (volume | suono) più forte) | (parla più forte);
<wander> : [vai] (a fare un giro| fare una passeggiata) | vattene;
<welcome> : benvenuto;
<show> : mostra [mi | ci] quando sei;
<sick> : <show> malato;
<sad> : <show> triste;
<lovely> : <show> (adorabile | amorevole);
<happy> : <show> (felice | gioioso | contento);
<tired> : <show> stanco;
<angry> : <show> nervoso;
<grumpy> : <show> lamentoso;
<scared> : <show> spaventato;
<delai> : <numbers#nb> (secondi | minuti | ore);
<rotationangle> : [di] <numbers#nb> gradi;
<operation> : [quanto (fa | faceva)]  <numbers#nb> (più | meno | diviso per | moltiplicato per | per) <numbers#nb>;
<operationagain> : (più | meno | diviso per | moltiplicato per | per) <numbers#nb>;
<quit> : togliere | annullare | arrestare | uscire | terminare | togli | chiudi | arresta | annulla | è tutto | stop;
