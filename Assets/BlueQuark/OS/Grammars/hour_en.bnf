#BNF+EMV2.0;
!grammar hour_en;


/*
**	Hour rules to export
*/

!export <hour_en#atnoon>;
!export <hour_en#atprenoon>;
!export <hour_en#atpostnoon>;
!export <hour_en#atmidnight>;
!export <hour_en#atpremidnight>;
!export <hour_en#atpostmidnight>;
!export <hour_en#athours>;
!export <hour_en#atprehours>;
!export <hour_en#atposthours>;
!export <hour_en#atamhours>;
!export <hour_en#atpreamhours>;
!export <hour_en#atpostamhours>;
!export <hour_en#atpmhours>;
!export <hour_en#atprepmhours>;
!export <hour_en#atpostpmhours>;

!start <atnoon>;
!start <atprenoon>;
!start <atpostnoon>;
!start <atmidnight>;
!start <atpremidnight>;
!start <atpostmidnight>;
!start <athours>;
!start <atprehours>;
!start <atposthours>;
!start <atamhours>;
!start <atpreamhours>;
!start <atpostamhours>;
!start <atpmhours>;
!start <atprepmhours>;
!start <atpostpmhours>;

<atnoon> : [at] noon;
<atprenoon> : [at] <prehours> noon;
<atpostnoon> : [at] noon <posthours>;
<atmidnight> : [at] midnight;
<atpremidnight> : [at] <prehours> midnight;
<atpostmidnight> : [at] midnight <posthours>;
<athours> : [at] <hours> [o'clock];
<atprehours> : [at] <prehours> <hours>;
<atposthours> : [at] <hours> <posthours>;
<atamhours> : [at] <morninghours> am;
<atpreamhours> : [at] <prehours> <morninghours> am;
<atpostamhours> : [at] <morninghours> <posthours> am;
<atpmhours> : [at] <morninghours> pm;
<atprepmhours> : [at] <prehours> <morninghours> pm;
<atpostpmhours> : [at] <morninghours> <posthours> pm;

<morninghours> : 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9 | 10 | 11;
<hours> : <morninghours> | 12 | 13 | 14 | 15 | 16 | 17 | 18 | 19 | 20 | 21 | 22 | 23;
<firstthirtyminutes> : 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9 | 10 | 11 | 12 | 13 | 14 | 15 | 16 | 17 | 18 | 19 | 20 | 21 | 22 | 23 | 24 | 25 | 26 | 27 | 28 | 29;
<minutes> : <firstthirtyminutes> | 30 | 31 | 32 | 33 | 34 | 35 | 36 | 37 | 38 | 39 | 40 | 41 | 42 | 43 | 44 | 45 | 46 | 47 | 48 | 49 | 50 | 51| 52 | 53 | 54 | 55 | 56 | 57 | 58 | 59;
<prehours> : <firstthirtyminutes> (to | past) | half [past] | [a] quarter (past | to);
<posthours> : [and | past] <minutes> [minute[s]] | and [a] half | and [a] quarter;